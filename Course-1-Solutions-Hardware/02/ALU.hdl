// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    And16(a=x, b=false, out=xandfalse);
    Mux16(a=x, b=xandfalse, sel=zx, out=outzx);

    Not16(in=outzx, out=notzx);
    Mux16(a=outzx, b=notzx, sel=nx, out=outnx);

    And16(a=y, b=false, out=yandfalse);
    Mux16(a=y, b=yandfalse, sel=zy, out=outzy);

    Not16(in=outzy, out=notzy);
    Mux16(a=outzy, b=notzy, sel=ny, out=outnz);

    Add16(a=outnx, b=outnz, out=xaddy);
    And16(a=outnx, b=outnz, out=xandy);
    Mux16(a=xandy, b=xaddy, sel=f, out=outf);

    Not16(in=outf, out=outno);
    // Below I also assign the value of the msb (left-most bit) to the ng output
    // The left-mest bit indicates if the number is negative (1 == negative)
    Mux16(a=outf, b=outno, sel=no, out=out, out[15]=ng, out[0..7]=zr1, out[8..15]=zr2);

    // Using an Or8Way for each the first 8 bits and last 8 bits of the 'out'.
    // With Or8Way I can test whether the bits in 'out' are 0 or 1.
    // If there is any 1's in 'out', then zr is zero, if there are no 1's, zr is 1.
    Or8Way(in=zr1, out=orzr1);
    Or8Way(in=zr2, out=orzr2);
    Or(a=orzr1, b=orzr2, out=or8ways);
    Mux(a=true, b=false, sel=or8ways, out=zr);

   
}